plugins {
    id "com.github.hierynomus.license" version "0.14.0"
    id 'java-gradle-plugin'
}

apply plugin: 'groovy'
apply plugin: 'maven'

group 'org.feedhenry.tools'
version '1.0-SNAPSHOT'

license {
    excludes (['**/*.json', "src/test/resources/**/*","src/main/resources/**/*"])
    header rootProject.file('codequality/HEADER')
    strictCheck false
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    google()
}


uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: uri('build/libs'))
        }
    }

}

dependencies {
    compile 'com.android.tools.build:gradle:3.0.0-beta6'
    testCompile 'junit:junit:4.12'
    compile 'org.codehaus.groovy:groovy-all:2.4.11'

}
configurations.all {
    resolutionStrategy {
        force 'org.codehaus.groovy:groovy-all:2.4.11'
    }
}

gradlePlugin {
    plugins {
        mcpAndroidPlugin {
            id = "mcp-android-plugin"
            implementationClass = "org.feedhenry.tools.android.FeedHenryAndroidMCPPlugin"
        }
    }
}
// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}